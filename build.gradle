buildscript{
  repositories{
    mavenCentral()
  }
  dependencies{
    classpath("org.springframework.boot:spring-boot-gradle-plugin:1.3.0.RELEASE")
  } 
}

ext{
  groovyVersion = '2.4.3'
}

allprojects{
  apply plugin:'groovy'
}

subprojects{
  repositories{
    jcenter()
    mavenCentral()
  }
  
  dependencies{
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "redis.clients:jedis:2.5.1"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
  }

}

task wrapper(type: Wrapper){
  gradleVersion = '2.4'
}

project(':core'){
  
  apply plugin: 'spring-boot'

  jar{
    baseName='smartOwl'
    version='0.1.0'
  }

  bootRepackage {
    mainClass = 'mx.ipn.ambienta2mx.smartOwl.Application' 
  }

  dependencies{
    compile project(':api')
    compile("org.springframework.boot:spring-boot-starter-web") 
    compile("org.springframework.boot:spring-boot-starter-actuator")
    compile("org.springframework.boot:spring-boot-starter-test")
    compile("org.springframework.boot:spring-boot-starter-redis")
    compile("org.jsoup:jsoup:1.7.2")
    compile("com.github.groovy-wslite:groovy-wslite:1.1.2")
  }

  bootRun{
    systemProperties = System.properties
  }

  test{
    exclude '**/*IntegrationSpec.class'
    reports.html.destination = file("$reports.html.destination/unit")
    reports.junitXml.destination = file("$reports.junitXml.destination/unit")
  }
  
  task integrationTest(type: Test){
    include '**/*IntegrationSpec.class'
    reports.html.destination = file("$reports.html.destination/integration")
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
  }

  check.dependsOn integrationTest

}

project(':api'){
  
}
