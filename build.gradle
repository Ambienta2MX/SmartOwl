ext{
  groovyVersion = '2.4.3'
}

allprojects{
  apply plugin:'groovy'
}

subprojects{
  repositories{
    jcenter()
    mavenCentral()
  }
  
  dependencies{
    compile "org.codehaus.groovy:groovy-all:${groovyVersion}"
    compile "redis.clients:jedis:2.5.1"
    compile "org.eclipse.jetty.aggregate:jetty-server:8.1.17.v20150415"
    compile "org.eclipse.jetty.aggregate:jetty-servlet:8.1.17.v20150415"
    compile "javax.servlet:javax.servlet-api:3.0.1"
    testCompile "org.spockframework:spock-core:1.0-groovy-2.4"
  }

}

task wrapper(type: Wrapper){
  gradleVersion = '2.4'
}

project(':core'){

  dependencies{
    compile project(':api')
  }

  test{
    exclude '**/*IntegrationSpec.class'
    reports.html.destination = file("$reports.html.destination/unit")
    reports.junitXml.destination = file("$reports.junitXml.destination/unit")
  }
  
  task integrationTest(type: Test){
    include '**/*IntegrationSpec.class'
    reports.html.destination = file("$reports.html.destination/integration")
    reports.junitXml.destination = file("$reports.junitXml.destination/integration")
  }

  task run(type:JavaExec){
    main = 'Main'
    classpath sourceSets.main.runtimeClasspath
  }

  check.dependsOn integrationTest

}

project(':api'){
  
}
